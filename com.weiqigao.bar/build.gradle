buildscript {
    repositories {
        maven {
            url 'https://plugins.gradle.org/m2/'
        }
    }

    dependencies {
        classpath 'org.javamodularity:moduleplugin:1.1.1'
    }
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'org.javamodularity.moduleplugin'

import java.util.regex.Matcher

mainClassName = "$moduleName/com.weiqigao.bar.Main"

repositories {
    mavenLocal()
}

dependencies {
    implementation 'com.weiqigao:foo:1.0.0'
}

compileJava {
    doFirst {
        def jsr305 = classpath.filter{it.name.contains('jsr305')}.asPath
        classpath = classpath.filter{!it.name.contains('jsr305')}
        options.compilerArgs += [
                '--patch-module', "java.annotation=$jsr305"
        ]
    }
}


run {
    doFirst {
        def jsr305 = classpath.filter{it.name.contains('jsr305')}.asPath
        classpath = classpath.filter{!it.name.contains('jsr305')}
        jvmArgs += [
                '--patch-module', "java.annotation=$jsr305"
        ]
    }
}

startScripts {
    doFirst {
        classpath = classpath.filter{!it.name.contains('jsr305')}
        defaultJvmOpts = [
                '--patch-module', "java.annotation=APP_HOME_PATCH/jsr305-3.0.2.jar"
        ]
    }

    doLast {
        def bashFile = new File(outputDir, applicationName)
        String bashContent = bashFile.text
        bashFile.text = bashContent.replaceFirst('APP_HOME_PATCH/', Matcher.quoteReplacement('$APP_HOME_/patch/'))

        def batFile = new File(outputDir, applicationName + ".bat")
        String batContent = batFile.text
        batFile.text = batContent.replaceFirst('APP_HOME_PATCH/', Matcher.quoteReplacement('%APP_HOME%\\patch\\'))
    }
}

distributions {
    main {
        contents {
            filesMatching ("*jsr305*") {
                setPath getPath().replaceFirst('lib', 'patch')
            }
        }
    }
}
